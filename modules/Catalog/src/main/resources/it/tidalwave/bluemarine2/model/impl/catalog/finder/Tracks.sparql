        SELECT DISTINCT *
        WHERE
          {
                       ?track        a                                  mo:Track.
                       ?track        bmmo:importedFrom                  ?source.
                       ?track        rdfs:label                         ?label.
                       ?track        mo:track_number                    ?track_number.
            OPTIONAL { ?track        bmmo:diskNumber                    ?disk_number.      }
            OPTIONAL { ?track        bmmo:diskCount                     ?disk_count.       }
@artist@             { ?track        foaf:maker                         ?artist.           }
@artist@    UNION    { ?track        foaf:maker/rel:collaboratesWith    ?artist.           }

                       ?record       a                                  mo:Record.
                       ?record       mo:track                           ?track.
                       ?record       rdfs:label                         ?record_label.
            MINUS    { bmmo:alternativeItems  bmmo:includes             ?record.           }

                       ?signal       a                                  mo:DigitalSignal.
                       ?signal       mo:published_as                    ?track.
                       ?signal       mo:duration                        ?duration.

                       ?audioFile    a                                  mo:AudioFile.
                       ?audioFile    mo:encodes                         ?signal.
                       ?audioFile    bmmo:path                          ?path.
            OPTIONAL { ?audioFile    bmmo:fileSize                      ?fileSize.         }
          }
        ORDER BY
            ?record_label
            ?disk_number
            ?track_number
            ?label
